#!/bin/tcsh
#
# Liverpool John Moores University
# GCN_Server loop startup script.
#
# GCN_Server startup script. This version is for use on proxy machines,
# the full version is available for deployments in ~dev/src/planetarium/scripts/ 
# This is designed to be run from the gcn /etc/init.d startup script.
#ident	"$Id: gcn_loop_start.grb,v 1.7 2008-07-25 15:51:30 cjm Exp $"
setenv GCN_HOME /grb/bin/planetarium/java
setenv GCN_PIDFILE /grb/tmp/gcn.pid
set date_string = `date +"%Y-%m-%dT%H:%M:%S"`
setenv GCN_OUPTUT_FILE "/grb/tmp/gcn_output-${date_string}.txt"
setenv GCN_LOOP_OUPTUT_FILE "/grb/tmp/gcn_loop_output.txt"
setenv GCN_SERVER_ARGS ""
# Configure port number based on machine hostname
# See: http://gcn.gsfc.nasa.gov/sites_cfg.html , Sites: LT, FTN, FTS
# Note, probably doesn't work on ltccd1, --short does not work!
# Also HOSTTYPE not present in /etc/init.d scripts?
set hostname = `/bin/hostname --short`
switch ( ${hostname} )
    case "ltccd1":
	setenv GCN_PORT_NUMBER "5169"
	breaksw
    case "ltproxy":
	setenv GCN_PORT_NUMBER "5212"
	breaksw
    case "ftnproxy":
	setenv GCN_PORT_NUMBER "5213"
	breaksw
    case "ftsproxy":
	setenv GCN_PORT_NUMBER "5214"
	breaksw
    default:
	echo "Unknown hostname ${hostname}."
	exit 1
	breaksw
endsw
setenv GCN_SERVER_ARGS "${GCN_SERVER_ARGS} -port ${GCN_PORT_NUMBER}"
setenv GCN_MAIL_ADDRESSES "cjm@astro.livjm.ac.uk snf@astro.livjm.ac.uk nrc@astro.livjm.ac.uk ltops@astro.livjm.ac.uk grb@astro.livjm.ac.uk"
# Some .sh equivalent stuff of environment in
# lt_environment.csh
setenv CLASSPATH ".:${GCN_HOME}"
set list = "/grb/bin/javalib/ngat_util.jar /grb/bin/javalib/ngat_util_logging.jar /grb/bin/javalib/ngat_astrometry.jar"
foreach dir ($list)
    setenv CLASSPATH ${CLASSPATH}":"$dir
end
setenv LD_LIBRARY_PATH /grb/bin/lib/${HOSTTYPE}

set done = 0
while ( ${done} == 0 )
  /bin/rm -f ${GCN_PIDFILE}
  cd ${GCN_HOME}
  echo "GCN Server starting." >>! ${GCN_LOOP_OUPTUT_FILE}
  cd ${GCN_HOME}
  (java GCN_Server ${GCN_SERVER_ARGS} >>! ${GCN_OUPTUT_FILE}) >>& ${GCN_OUPTUT_FILE} &
  echo $! > ${GCN_PIDFILE}
  set pid = `cat ${GCN_PIDFILE}`
  set date_string = `date +"%Y/%m/%dT%H.%M.%S"`
  echo "${date_string} GCN Server started on ${hostname}:${GCN_PORT_NUMBER} with PID ${pid}." >>! ${GCN_LOOP_OUPTUT_FILE}
  echo "Subject: GCN_Server\n${date_string} GCN Server started on ${hostname}:${GCN_PORT_NUMBER} with PID ${pid}.\n" | mail ${GCN_MAIL_ADDRESSES}
  wait
  set date_string = `date +"%Y/%m/%dT%H.%M.%S"`
  echo "${date_string} The GCN Server PID ${pid} has stopped." >>! ${GCN_LOOP_OUPTUT_FILE}
  echo "Subject: GCN_Server\n${date_string} The GCN Server on ${hostname}:${GCN_PORT_NUMBER} with PID ${pid} has stopped.\n" | mail ${GCN_MAIL_ADDRESSES}
end

